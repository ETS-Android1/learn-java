apply plugin: 'com.android.application'

if(project.hasProperty("AndroidProjects.signing")
    && new File(project.property("AndroidProjects.signing") + "androidsigning.gradle").exists()) {
  apply from: project.property("AndroidProjects.signing") + "androidsigning.gradle"
}

/*
 * Contains not only ad related data, but also API keys.
 */
if(project.hasProperty("AndroidProjects.ads")
        && new File(project.property("AndroidProjects.ads") + "learnjava_ads.gradle").exists()) {
    apply from: project.property("AndroidProjects.ads") + "learnjava_ads.gradle"
}

android {
    compileSdkVersion 30
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "com.gaspar.learnjava"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 18
        versionName "3.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release { //ONLY I CAN BUILD this variant: actual release build
            minifyEnabled false
            resValue "bool", "is_debug", "false"
            resValue "bool", "load_ads", "true"
            resValue "bool", "playground_enabled", "true"
			//real signing configs and ad IDs are only present on my computer
            //real API keys are only present on my computer
        }
        debug { //ONLY I CAN BUILD this variant: debug mode which loads debug ads.
            debuggable true
            resValue "bool", "is_debug", "true"
            resValue "bool", "load_ads", "true"
            resValue "bool", "playground_enabled", "true"
            signingConfig signingConfigs.debug
			//real signing configs and ad IDs are only present on my computer
            //real API keys are only present on my computer
        }
        noads { //EVERYBODY can build this variant. Same as debug, but with no ads at all.
            debuggable true
            resValue "bool", "is_debug", "true"
            resValue "bool", "load_ads", "false"
            resValue "bool", "playground_enabled", "true"
            signingConfig signingConfigs.debug
            //fake IDs so the compilation does not fail. otherwise unused
            resValue "string", "ad_mob_app_id", "ca-app-pub-3940256099942544~3347511713" //sample app ID from ad mob website
            resValue "string", "ad_unit_id_banner_test", "fake_id"
            resValue "string", "ad_unit_id_banner_courses", "fake_id"
            resValue "string", "ad_unit_id_banner_exams", "fake_id"
            resValue "string", "ad_unit_id_banner_tasks", "fake_id"
            resValue "string", "ad_unit_id_interstitial_test", "fake_id"
            resValue "string", "ad_unit_id_interstitial_chapter", "fake_id"
            resValue "string", "ad_unit_id_interstitial_task", "fake_id"
            //fake API keys
            resValue "string", "run_api_key_1", "mock_api_key"
            resValue "string", "run_api_key_2", "mock_api_key"
            resValue "string", "run_api_key_3", "mock_api_key"
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    useLibrary 'android.test.runner'
    useLibrary 'android.test.base'
    useLibrary 'android.test.mock'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation "androidx.preference:preference:1.1.1"
    implementation "android.arch.persistence.room:runtime:1.1.1"

    implementation 'com.github.iwgang:countdownview:2.1.6'
    annotationProcessor "android.arch.persistence.room:compiler:1.1.1"
    implementation 'com.google.android.gms:play-services-ads:20.2.0'
    implementation 'com.kaopiz:kprogresshud:1.0.2'
    implementation 'com.techyourchance:threadposter:1.0.1'
    implementation "com.squareup.retrofit2:converter-moshi:2.4.0"
    implementation 'org.greenrobot:eventbus:3.2.0'
    implementation 'com.github.yshrsmz:KeyboardVisibilityEvent:v3.0.0-RC3'

    // ------------------ TEST dependencies
    testImplementation 'junit:junit:4.13.2'

    // Core library
    androidTestImplementation 'androidx.test:core:1.4.0'

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'

    // Assertions
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'junit:junit:4.13.2'

    // Espresso dependencies
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.4.0'
    androidTestImplementation 'androidx.test.espresso.idling:idling-concurrent:3.4.0'

    //UI automator
    androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.3'
}
